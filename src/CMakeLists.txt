cmake_minimum_required(VERSION 3.20)

project(crystal-dock)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# When doing code clean-up, uncomment this.
# add_compile_options(-Wall -Werror)
# When doing code clean-up, comment this.
add_definitions(-DQT_NO_DEPRECATED_WARNINGS)

set(CMAKE_INCLUDE_CURRENT_DIR ON)
set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTOUIC ON)

find_package(Qt6 6.6 REQUIRED COMPONENTS DBus Gui Test Widgets)
find_package(Wayland 1.22 REQUIRED COMPONENTS Client)
find_package(LayerShellQt 6.0 REQUIRED)

set(SRCS
    desktop/desktop_env.cc
    display/window_system.cc
    display/wlr_foreign_toplevel_management.c
    display/wlr_window_manager.cc
    model/application_menu_config.cc
    model/config_helper.cc
    model/multi_dock_model.cc
    view/add_panel_dialog.cc
    view/appearance_settings_dialog.cc
    view/application_menu_settings_dialog.cc
    view/application_menu.cc
    view/clock.cc
    view/color_button.cc
    view/desktop_selector.cc
    view/dock_item.cc
    view/dock_panel.cc
    view/icon_based_dock_item.cc
    view/iconless_dock_item.cc
    view/multi_dock_view.cc
    view/program.cc
    view/separator.cc
    view/task_manager_settings_dialog.cc
    utils/desktop_file.cc
    desktop/desktop_env.h
    display/window_system.h
    display/wlr_foreign_toplevel_management.h
    display/wlr_window_manager.h
    model/application_menu_config.h
    model/application_menu_entry.h
    model/config_helper.h
    model/multi_dock_model.h
    view/add_panel_dialog.h
    view/appearance_settings_dialog.h
    view/application_menu_settings_dialog.h
    view/application_menu.h
    view/clock.h
    view/color_button.h
    view/desktop_selector.h
    view/dock_item.h
    view/dock_panel.h
    view/icon_based_dock_item.h
    view/iconless_dock_item.h
    view/multi_dock_view.h
    view/program.h
    view/separator.h
    view/task_manager_settings_dialog.h
    utils/command_utils.h
    utils/desktop_file.h
    utils/draw_utils.h
    utils/font_utils.h
    utils/icon_utils.h
    utils/math_utils.h
    utils/menu_utils.h
    view/add_panel_dialog.ui
    view/appearance_settings_dialog.ui
    view/application_menu_settings_dialog.ui
    view/task_manager_settings_dialog.ui)
add_library(crystal-dock_lib STATIC ${SRCS})

set(LIBS Qt6::DBus Qt6::GuiPrivate Qt6::Widgets Wayland::Client LayerShellQt::Interface)
target_link_libraries(crystal-dock_lib ${LIBS})

add_executable(crystal-dock main.cc)
target_link_libraries(crystal-dock crystal-dock_lib ${LIBS})

configure_file(crystal-dock.desktop.cmake
    ${CMAKE_CURRENT_BINARY_DIR}/crystal-dock.desktop @ONLY)

# Install

install(TARGETS crystal-dock RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR})
install(FILES ${CMAKE_CURRENT_BINARY_DIR}/crystal-dock.desktop
    DESTINATION ${CMAKE_INSTALL_PREFIX}/share/applications)

# Uninstall

configure_file(
    "${CMAKE_CURRENT_SOURCE_DIR}/cmake_uninstall.cmake.in"
    "${CMAKE_CURRENT_BINARY_DIR}/cmake_uninstall.cmake"
    @ONLY)

add_custom_target(uninstall COMMAND ${CMAKE_COMMAND}
    -P ${CMAKE_CURRENT_BINARY_DIR}/cmake_uninstall.cmake)

# Test

enable_testing()

add_executable(application_menu_config_test model/application_menu_config_test.cc)
target_link_libraries(application_menu_config_test Qt6::Test crystal-dock_lib ${LIBS})
add_test(application_menu_config_test application_menu_config_test)
